import groovy.json.JsonSlurper

//JsonSlurper is the module for parsing json text or reader content into Groovy Data Structures

String basePath = 'multipleBranches'
String repo = 'sheehan/grails-example'

folder(basePath) {
    description 'This example shows how to create a set of jobs for each github branch, each in its own folder.'
}

//the below line fetches the list of branch url from the repos and the subsequent line parses it and stores in in Groovy DS
URL branchUrl = "https://api.github.com/repos/$repo/branches".toURL()
List branches = new JsonSlurper().parse(branchUrl.newReader())
branches.each { branch ->
    // replaces / with -
    String safeBranchName = branch.name.replaceAll('/', '-')

    // this function creates the updates folder in jenkins
    folder("$basePath/$safeBranchName")
    {
        description("Folder containing all jobs for $basePath/$safeBranchName")
    }

    job("$basePath/$safeBranchName/grails-example-build") {
        scm {
            github repo, branch.name
        }
        triggers {
            scm 'H/30 * * * *'
        }
        steps {
            grails 'test-app war', true
        }
    }

    job("$basePath/$safeBranchName/grails-example-deploy") {
        parameters {
            stringParam 'host'
        }
        steps {
            shell 'scp war file; restart...'
        }
    }
}
